# Maintainer: Israel Jacquez <mrkotfw@gmail.com>

_branch="master"
_realname=yaul-tool-chain-git
pkgname=${_realname}
pkgver=r117.722cc4d
pkgrel=1
pkgdesc="Tool-chain for Yaul"
arch=('x86_64')
url="https://yaul.org/"
license=('MIT')
depends=("gawk" "sed" "make")
makedepends=("make" "gcc" "unzip" "help2man" "wget" "git")
options=('!strip' '!buildflags' 'staticlibs' 'debug')
source=("git+https://github.com/ijacquez/libyaul-build-scripts#branch=${_branch}" "local://yaul.sh")
sha256sums=('SKIP' 'SKIP')

pkgver() {
  cd "${srcdir}/libyaul-build-scripts"

  printf -- "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse HEAD | sed -r 's/(.{7}).*/\1/')"
}

prepare() {
  cd "${srcdir}/libyaul-build-scripts"
  /usr/bin/git submodule init
  /usr/bin/git submodule update -f

  /bin/rm -f .config
  /bin/cp sh2eb-elf/native-linux.config .config
  /bin/sed -i -E 's#^(CT_DEBUG_CT)=.*#\1=n#g' .config
  /bin/sed -i -E 's#^(CT_DEBUG_INTERACTIVE)=.*#\1=n#g' .config
  /bin/sed -i -E 's#^(CT_LOCAL_TARBALLS_DIR)=.*#\1='${srcdir}'/tarballs#g' .config
  /bin/sed -i -E 's#^(CT_PREFIX_DIR)=.*$#\1="${CT_PREFIX:-'"${srcdir}/opt/tool-chains"'}/${CT_HOST:+HOST-${CT_HOST}/}${CT_TARGET}"#g' .config
  /bin/sed -i -E 's#^(CT_LOG_TO_FILE)=.*#\1=n#g' .config
  /bin/sed -i -E 's#^(CT_LOG_FILE_COMPRESS)=.*#\1=n#g' .config

  /bin/mkdir -p "${srcdir}/tarballs"
  /bin/mkdir -p "${srcdir}/opt/tool-chains"
}

build() {
  cd "${srcdir}/libyaul-build-scripts"

  # Crosstool-NG reports an error: Don't set CFLAGS. It screws up the build.
  unset CFLAGS
  # Crosstool-NG reports an error: Don't set CXXFLAGS. It screws up the build.
  unset CXXFLAGS

  cd crosstool-ng
  ./bootstrap
  ./configure --enable-local
  /usr/bin/make
  cd ..

  crosstool-ng/ct-ng build
}

package() {
  /bin/cp -r "${srcdir}/opt" "${pkgdir}/"

  /usr/bin/mkdir -p "${pkgdir}/etc/profile.d"
  /usr/bin/install -m 644 "yaul.sh" "${pkgdir}/etc/profile.d/yaul.sh"
}

# -*- mode: sh -*-

# Local Variables:
# mode: sh
# sh-basic-offset: 2
# End:
