# Maintainer: Israel Jacquez <mrkotfw@gmail.com>

_branch="develop"
_realname=libyaul-git

pkgbase=${MINGW_PACKAGE_PREFIX}-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=r2003.004fc14
pkgrel=1
pkgdesc="An open source development kit for the SEGA Saturn (mingw-w64)"
arch=('x86_64')
url="http://yaul.org/"
license=('MIT')
depends=("make" "mingw-w64-x86_64-yaul-tool-chain")
builddepends=("git" "make" "gcc" "diffutils" "tar" "gawk" "sed" "findutils" "mingw-w64-x86_64-yaul-tool-chain")
options=('!strip' '!buildflags' 'staticlibs' 'debug')
source=("${_realname}"::"git+https://github.com/ijacquez/libyaul#branch=${_branch}")
sha256sums=('SKIP')
install=yaul.install

_change_env_value() {
  local _variable="${1}"
  local _value="${2}"
  local _out="${3}"

  /usr/bin/awk -F '=' '/^export '"${_variable}"'=.+/ { print $1 "='"${_value}"'"; getline; } { print; }' "${_out}" > "${_out}.tmp"
  /usr/bin/mv "${_out}.tmp" "${_out}"
}

pkgver() {
  cd "${srcdir}/${_realname}"

  printf -- "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse HEAD | sed -r 's/(.{7}).*/\1/')"
}

prepare() {
  cd "${srcdir}/${_realname}"

  git submodule init
  git submodule update -f

  cp -v yaul.env.in "${srcdir}/yaul.env"
  _change_env_value "YAUL_INSTALL_ROOT" "/opt/tool-chains/sh2eb-elf" "${srcdir}/yaul.env"
  _change_env_value "YAUL_PREFIX" "sh2eb-elf" "${srcdir}/yaul.env"
  _change_env_value "YAUL_BUILD_ROOT" "${srcdir}" "${srcdir}/yaul.env"
  _change_env_value "YAUL_BUILD" "build" "${srcdir}/yaul.env"
  _change_env_value "YAUL_OPTION_DEV_CARTRIDGE" "2" "${srcdir}/yaul.env"
  _change_env_value "YAUL_CDB" "0" "${srcdir}/yaul.env"

  cp -v yaul.env.in "${srcdir}/yaul.env.user"
  _change_env_value "YAUL_BUILD_ROOT" "\$HOME" "${srcdir}/yaul.env.user"
}

build() {
  cd "${srcdir}/${_realname}"

  source "${srcdir}/yaul.env"

  mkdir -p "${YAUL_INSTALL_ROOT}"

  if check_option "debug" "y"; then
    SILENT=1 /usr/bin/make -j 1 debug tools
  else
    SILENT=1 /usr/bin/make -j 1 release tools
  fi
}

check() {
  :
}

package() {
  cd "${srcdir}/${_realname}"

  source "${srcdir}/yaul.env"

  export YAUL_INSTALL_ROOT="${pkgdir}/opt/tool-chains/sh2eb-elf"

  /usr/bin/mkdir -p "${pkgdir}/opt/tool-chains/sh2eb-elf"

  if check_option "debug" "y"; then
    SILENT=1 /usr/bin/make -j 1 install-debug install-tools
  else
    SILENT=1 /usr/bin/make -j 1 install-release install-tools
  fi

  /usr/bin/install -m 644 "${srcdir}/yaul.env.user" "${pkgdir}/opt/tool-chains/sh2eb-elf/yaul.env.in"

  # Remove references to ${pkgdir}
  /usr/bin/find "${pkgdir}/opt/tool-chains/sh2eb-elf" -type f -exec /usr/bin/sed -E -i 's#'"${pkgdir}"'##g' '{}' \;
}

# -*- mode: sh -*-

# Local Variables:
# mode: sh
# sh-basic-offset: 2
# End:
